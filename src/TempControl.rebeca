// Write your code here !
// Don't tell me what to do!

// Temperature constants
env int desiredTemp_const = 22;
env int threshold_low_const = 21;
env int threshold_high_const = 23;

// Polling constants
env int sensorPoll_const = 2;
env int tickPoll_const = 10;
env int regulationPoll_const = 5;


reactiveclass Controller(5) {
    knownrebecs { HC_Unit hc_unit; }

    statevars { 
        int currentTemp;
        boolean isHeating;
        boolean isCooling;
    }

    Controller() {
        isCooling = false;
        isHeating = false;
        currentTemp = desiredTemp_const; // Start off with desired
    }

    msgsrv setCurrent(int value) {
        currentTemp = value;
        self.updateOutput();
    }

    msgsrv updateOutput() {
        if (currentTemp >= threshold_low_const && currentTemp <= threshold_high_const) {
            hc_unit.switchoff();
            isCooling = false;
            isHeating = false;
        }
        else if (currentTemp < threshold_low_const) {
            hc_unit.setHeating();
            isHeating = true;
            isCooling = false;
        }
        else if (currentTemp > threshold_high_const) {
            hc_unit.setCooling();
            isCooling = true;
            isHeating = false;
        }
    }
}

reactiveclass Sensor(5) {
    knownrebecs { 
        Room room;
        Controller controller;
    }

    Sensor() {
        self.callSensor();
    }

    msgsrv callSensor() {
        room.getTemp();
        self.callSensor() after(sensorPoll_const);
    }

    msgsrv setCurrent(int value) {
        controller.setCurrent(value);
    }
}

reactiveclass Room(5) {
    knownrebecs { Sensor sensor; }

    statevars { 
        int currentTemp;
        int currentRegulation;
        int outside_air;
     }

    Room() {
        currentTemp = desiredTemp_const;
        currentRegulation = 0;
        outside_air = 0;
        self.updateStatus();
    }

    msgsrv updateStatus() {
        outside_air = ?(1, 0, -1);
        currentTemp = currentTemp - outside_air + currentRegulation;
        self.updateStatus() after(tickPoll_const);
    }

    msgsrv setRegulation(int value) {
        currentRegulation = value;
    }

    msgsrv getTemp() {
        sensor.setCurrent(currentTemp);
    }
}

reactiveclass HC_Unit(5) {
    knownrebecs { Room room; }

    statevars { 
        boolean heaterOn;
        boolean coolerOn;
        int currentRegulation;
    }

    HC_Unit() {
        heaterOn = false;
        coolerOn = false;
        currentRegulation = 0;
        self.regulateTemp();
    }

    msgsrv setCooling() {
        currentRegulation = -1;
        heaterOn = false;
        coolerOn = true;
    }

    msgsrv setHeating() {
        currentRegulation = 1;
        heaterOn = true;
        coolerOn = false;
    }

    msgsrv switchoff() {
        currentRegulation = 0;
        coolerOn = false;
        heaterOn = false;
    }

    msgsrv regulateTemp() {
        room.setRegulation(currentRegulation);
        self.regulateTemp() after(regulationPoll_const);
    }
}

main {
    Room room(sensor): ();
    Controller controller(hc_unit): ();
    Sensor sensor(room, controller): ();
    HC_Unit hc_unit(room): ();
}