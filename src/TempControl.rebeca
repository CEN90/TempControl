//Write your code here !
// Don't tell me what to do!

// Temperature constants
env int desiredTemp_const = 22;
env int threshold_low_const = 21;
env int threshold_high_const = 23;

// Polling constants
env int sensorPoll_const = 2;
env int tickPoll_const = 10;
env int regulationPoll_const = 5;

reactiveclass Room(5) {
    knownrebecs { Sensor sensor; }

    statevars { 
        int currentTemp;
        int currentRegulation;
        int outside_air;
        int tickValue;
     }

    Room() {
        currentTemp = desiredTemp_const;
        currentRegulation = 0;
        outside_air = 0;
        tickValue = tickPoll_const;
    }

    msgsrv updateStatus() {
        outside_air = ?(1, 0, -1);
        currentTemp = currentTemp - outside_air + currentRegulation;
        self.updateStatus() after(tickValue);
    }

    msgsrv setRegulation(int value) {
        // sensor.setCurrent(currentTemp);
        currentRegulation = value;
    }

    msgsrv getTemp() {
        sensor.setCurrent(currentTemp);
    }
}

reactiveclass Controller(5) {
    knownrebecs { HC_Unit hc_unit; }

    statevars { 
        int currentTemp;
        int desiredTemp;
        int threshold_low;
        int threshold_high;
        boolean isHeating;
        boolean isCooling;
     }

    Controller() {
        isCooling = false;
        isHeating = false;
        threshold_low = threshold_low_const;
        threshold_high = threshold_high_const;
        currentTemp = desiredTemp_const; // Start off with desired
    }

    msgsrv setCurrent(int value) {
        currentTemp = value;
        self.updateOutput();
    }

    msgsrv updateOutput() {
        if (currentTemp >= threshold_low && currentTemp <= threshold_high) {
            hc_unit.switchoff();
            isCooling = false;
            isHeating = false;
        }
        else if (currentTemp < threshold_low) {
            hc_unit.setHeating();
            isHeating = true;
            isCooling = false;
        }
        else if (currentTemp > threshold_high) {
            hc_unit.setCooling();
            isCooling = true;
            isHeating = false;
        }
    }
}

reactiveclass Sensor(5) {
    knownrebecs { 
        Room room;
        Controller controller;
    }

    Sensor() {
        callSensor();
    }

    msgsrv callSensor() {
        room.getTemp();
        self.callSensor() after(sensorPoll_const);
    }

    msgsrv setCurrent(int value) {
        controller.setCurrent(value);
    }
}

reactiveclass HC_Unit(5) {
    knownrebecs { Room room; }

    statevars { 
        boolean heaterOn;
        boolean coolerOn;
        int currentRegulation;
    }

    HC_Unit() {
        heaterOn = false;
        coolerOn = false;
        currentRegulation = 0;
        self.regulateTemp();
    }

    msgsrv setCooling() {
        currentRegulation = 1;
        heaterOn = false;
        coolerOn = true;
    }

    msgsrv setHeating() {
        currentRegulation = -1;
        heaterOn = true;
        coolerOn = false;
    }

    msgsrv switchoff() {
        currentRegulation = 0;
        coolerOn = false;
        heaterOn = false;
    }

    msgsrv regulateTemp() {
        room.setRegulation(currentRegulation);
        self.regulateTemp() after(regulationPoll_const);
    }

}

main {
    Room room(sensor): ();
    Controller controller(hc_unit): ();
    Sensor sensor(room, controller): ();
    HC_Unit hc_unit(room): ();
}