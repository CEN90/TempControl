// Write your code here !
// Don't tell me what to do!

// Temperature constants
env int desiredTemp_const = 22;
env int threshold_low_const = 21;
env int threshold_high_const = 23;

// Polling constants
env int sensorPoll_const = 2;
env int tickPoll_const = 10;
env int regulationPoll_const = 5;

// MitM-Attack on Sensor
env boolean attack_sensor = true;
env int sensor_attack_wait = 5;
env int sensor_inj_value = 24;

// Fabrication attack on Controller
env boolean attack_controller = true;
env int controller_attack_wait_const = 10;
env int controller_attack_func_const = 1; // 1: switchoff, 2: setCooling, 3: setHeating


reactiveclass Controller(5) {
    knownrebecs { HC_Unit hc_unit; }

    statevars { 
        int currentTemp;
        boolean isHeating;
        boolean isCooling;
    }

    Controller(int controller_attack_wait, int controller_attack_func) {
        isCooling = false;
        isHeating = false;
        currentTemp = desiredTemp_const; // Start off with desired

        if (attack_controller) {
            self.compromise(controller_attack_func) after(controller_attack_wait);
        }
    }

    msgsrv setCurrent(int value) {
        currentTemp = value;
        self.updateOutput();
    }

    msgsrv updateOutput() {
        if (currentTemp >= threshold_low_const && currentTemp <= threshold_high_const) {
            if (isCooling || isHeating) {
                hc_unit.switchoff();
                isCooling = false;
                isHeating = false;
            }
        }
        else if (currentTemp < threshold_low_const) {
            if (!isHeating) {
                hc_unit.setHeating();
                isHeating = true;
                isCooling = false;
            }
        }
        else if (currentTemp > threshold_high_const) {
            if (!isCooling) {
                hc_unit.setCooling();
                isCooling = true;
                isHeating = false;
            }
        }
    }

    msgsrv compromise(int controller_attack_func) {
        if (controller_attack_func == 1) {
            hc_unit.switchoff();
        } else if (controller_attack_func == 2) {
            hc_unit.setCooling();
        } else if (controller_attack_func == 3) {
            hc_unit.setHeating();
        }
    }
}

reactiveclass Sensor(5) {
    knownrebecs { 
        Room room;
        Controller controller;
    }

    Sensor() {
        self.callSensor();
    }

    msgsrv callSensor() {
        room.getTemp();
        self.callSensor() after(sensorPoll_const);
    }

    msgsrv setCurrent(int value) {
        controller.setCurrent(value);
    }
}

reactiveclass Room(5) {
    knownrebecs { Sensor sensor; }

    statevars { 
        int currentTemp;
        int currentRegulation;
        int outside_air;
     }

    Room() {
        currentTemp = desiredTemp_const;
        currentRegulation = 0;
        outside_air = 0;
        self.updateStatus();
    }

    msgsrv updateStatus() {
        outside_air = ?(1, 0, -1);
        currentTemp = currentTemp - outside_air + currentRegulation;
        self.updateStatus() after(tickPoll_const);
    }

    msgsrv setRegulation(int value) {
        currentRegulation = value;
    }

    msgsrv getTemp() {
        sensor.setCurrent(currentTemp);
    }
}

reactiveclass HC_Unit(5) {
    knownrebecs { Room room; }

    statevars { 
        boolean heaterOn;
        boolean coolerOn;
        int currentRegulation;
    }

    HC_Unit() {
        heaterOn = false;
        coolerOn = false;
        currentRegulation = 0;
        self.regulateTemp();
    }

    msgsrv setCooling() {
        currentRegulation = -1;
        heaterOn = false;
        coolerOn = true;
    }

    msgsrv setHeating() {
        currentRegulation = 1;
        heaterOn = true;
        coolerOn = false;
    }

    msgsrv switchoff() {
        currentRegulation = 0;
        coolerOn = false;
        heaterOn = false;
    }

    msgsrv regulateTemp() {
        room.setRegulation(currentRegulation);
        self.regulateTemp() after(regulationPoll_const);
    }
}

main {
    Room room(sensor): ();
    Controller controller(hc_unit): (controller_attack_wait_const, controller_attack_func_const);
    Sensor sensor(room, controller): ();
    HC_Unit hc_unit(room): ();
}