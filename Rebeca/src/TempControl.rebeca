// Temperature constants
env int desiredTemp_const = 25;

// thresholds for heating
env int thresh_heat = 24;

// thresholds for cooling
env int thresh_cool = 26;

// Hard boundaries
env int MAX_TEMP = 30;
env int MIN_TEMP = 20;

// Polling constants
env int SENSOR_DELAY = 2;
env int ROOM_DELAY = 10;
env int REGULATION_DELAY = 5;


reactiveclass Controller(5) {
    knownrebecs { HC_Unit hc_unit; }

    statevars { 
        int currentTemp;
        boolean isHeating;
        boolean isCooling;
    }

    Controller() {
        isCooling = false;
        isHeating = false;
        currentTemp = desiredTemp_const; // Start off with desired
    }

    msgsrv setCurrent(int value) {
        currentTemp = value;
        self.updateOutput();
    }

    msgsrv updateOutput() {
        if (currentTemp == desiredTemp_const) {
            if (isCooling || isHeating) {
                hc_unit.switchoff();
                isCooling = false;
                isHeating = false;
            }
        }
        else if (currentTemp < desiredTemp_const) {
            if (!isHeating) {
                hc_unit.setHeating();
                isHeating = true;
                isCooling = false;
            }
        }
        else if (currentTemp > desiredTemp_const) {
            if (!isCooling) {
                hc_unit.setCooling();
                isCooling = true;
                isHeating = false;
            }
        }
    }
}

reactiveclass Sensor(5) {
    knownrebecs { 
        Room room;
        Controller controller;
    }

    Sensor() {
        self.callSensor();
    }

    msgsrv callSensor() {
        room.getTemp();
        self.callSensor() after(SENSOR_DELAY);
    }

    msgsrv setCurrent(int value) {
        controller.setCurrent(value);
    }
}

reactiveclass Room(5) {
    knownrebecs { Sensor sensor; }

    statevars { 
        int currentTemp;
        int currentRegulation;
        int outside_air;
     }

    Room() {
        currentTemp = desiredTemp_const;
        currentRegulation = 0;
        outside_air = 0;
        self.start();
    }

    msgsrv start() {
        currentTemp = ?(20, 21, 22, 23, 24, 25, 26);
        self.updateStatus();
    }

    msgsrv updateStatus() {
        outside_air = ?(1, 0, -1);
        currentTemp = currentTemp - outside_air + currentRegulation;
        self.updateStatus() after(ROOM_DELAY);
    }

    msgsrv setRegulation(int value) {
        currentRegulation = value;
    }

    msgsrv getTemp() {
        sensor.setCurrent(currentTemp);
    }
}

reactiveclass HC_Unit(5) {
    knownrebecs { Room room; }

    statevars { 
        boolean heaterOn;
        boolean coolerOn;
        int currentRegulation;
    }

    HC_Unit() {
        heaterOn = false;
        coolerOn = false;
        currentRegulation = 0;
        self.regulateTemp();
    }

    msgsrv setCooling() {
        currentRegulation = -1;
        heaterOn = false;
        coolerOn = true;
    }

    msgsrv setHeating() {
        currentRegulation = 1;
        heaterOn = true;
        coolerOn = false;
    }

    msgsrv switchoff() {
        currentRegulation = 0;
        heaterOn = false;
        coolerOn = false;
    }

    msgsrv regulateTemp() {
        room.setRegulation(currentRegulation);
        self.regulateTemp() after(REGULATION_DELAY);
    }
}

main {
    Room room(sensor): ();
    Controller controller(hc_unit): ();
    Sensor sensor(room, controller): ();
    HC_Unit hc_unit(room): ();
}